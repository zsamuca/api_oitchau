(EmployeeUuid as text, DataConsulta as date) =>
let
    Host = "https://api.oitchau.com.br",
    Token = Token_Oitchau,

    Response =
        Json.Document(
            Web.Contents(
                Host,
                [
                    RelativePath = "v2/public/integrations_api/employees/hoursbank/totals",
                    Query = [
                        employeeUuid = EmployeeUuid,
                        date = Date.ToText(DataConsulta, "yyyy-MM-dd")
                    ],
                    Headers = [
                        Authorization = "Bearer " & Token,
                        Accept = "application/json"
                    ]
                ]
            )
        ),

    TotalsAny = try Response[totals] otherwise null,

    // transforma TotalsAny em uma lista de "carteiras" (cada item pode ser número ou record)
    Carteiras =
        if TotalsAny = null then
            {}
        else if Value.Is(TotalsAny, type record) then
            Record.FieldValues(TotalsAny)
        else if Value.Is(TotalsAny, type list) then
            TotalsAny
        else
            { TotalsAny },   // se vier número direto (0, -121, etc.)

    ToNumber = (x as any) as number =>
        let n = try Number.From(x) otherwise null
        in if n = null then 0 else n,

    SomaCarteira = (c as any) as number =>
        if c = null then
            0
        else if Value.Is(c, type record) then
            List.Sum(List.Transform(Record.FieldValues(c), each ToNumber(_)))
        else
            ToNumber(c),

    SaldoMinutos = List.Sum(List.Transform(Carteiras, each SomaCarteira(_)))
in
    SaldoMinutos
